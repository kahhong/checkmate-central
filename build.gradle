plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.2'
	id 'io.spring.dependency-management' version '1.1.6'
	id "com.github.node-gradle.node" version "7.1.0"
}

group = 'com.ila'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

node {
	download = false
	workDir = file("${project.projectDir}/src/main/frontend/checkmate-central-react/nodejs")
	npmWorkDir = file("${project.projectDir}/src/main/frontend/checkmate-central-react/npm")
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot starters
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

	// https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-api
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	// https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-impl
	implementation 'io.jsonwebtoken:jjwt-impl:0.12.6'
    // https://mvnrepository.com/artifact/io.jsonwebtoken/jjwt-jackson
    implementation 'io.jsonwebtoken:jjwt-jackson:0.12.6'



	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Development tools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// Database
	runtimeOnly 'com.h2database:h2'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.junit.jupiter:junit-jupiter'
}

tasks.named('test') {
	useJUnitPlatform()
}

tasks.register('appNpmInstall', NpmTask) {
	workingDir = file("$projectDir/src/main/frontend/checkmate-central-react")
	args = ["install", "-debug"]
}

tasks.register('appNpmBuild', NpmTask) {
	workingDir = file("$projectDir/src/main/frontend/checkmate-central-react")
	if(System.getProperty("os.name").toLowerCase().contains("windows")) {
		args = ["run", "build_windows"]
	} else {
		args = ["run", "build_linux"]
	}
}

appNpmBuild.dependsOn(appNpmInstall)

// should run when building jar for production
bootJar.dependsOn(appNpmBuild)

// should run when starting development server locally
bootRun.dependsOn(appNpmBuild)